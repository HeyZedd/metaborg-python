module Statement

imports
  src-gen/signatures/-
  ast-syntax/-

  trans/desugar/Expression
  
rules
  desugar-suite: Statement(stmt) -> <desugar-stmt> stmt
  desugar-suite: BlockLine(stmts) -> <mapconcat(desugar-stmt)> stmts
  
  desugar-stmt: SimpleStatement(stmts, _) -> <map(desugar-stmt)> stmts
  desugar-stmt: Expression(exp) -> ExprStmt(<desugar-expr(!Load())> exp)
  
  desugar-stmt: For(For(trgt, iter, _, body, else)) -> [For(<desugar-for> trgt, <desugar-expr(!Load())> iter, <desugar-suite> body, <desugar-if> else)]
  desugar-stmt: While(cond, _, body, else) -> [While(<desugar-expr(!Load())> cond, <desugar-suite> body, <desugar-if> else)]
  desugar-stmt: If(cond, _, body, elif, else) -> [If(<desugar-expr(!Load())> cond, <desugar-suite> body, des-else)]
    where
      des-else := <foldr(!<desugar-if> else, desugar-if)> elif

  desugar-if: None() -> None()
  desugar-if: Some(item) -> <desugar-if> item
  desugar-if: Else(_, _, suite) -> <desugar-suite> suite
  
  desugar-if: (Elif(_, cond, _, body), else) -> If(<desugar-expr(!Load())> cond, <desugar-suite> body, else)
  desugar-for: [elem] -> <desugar-expr(!Load())> elem
  desugar-for: List(list, _) -> Tuple(<map(desugar-expr(!Load()))> list, Store())


  desugar-stmt: ClassDef(name, params, _, body) -> [ClassDef(ID(name), bases, keywords, <desugar-suite> body, decorators)]
  	where
  	  (bases, keywords) := <desugar-class> params;
  	  decorators := []
  desugar-class: None() -> ([], [])
  desugar-class: Some(list) -> <desugar-class> list
  desugar-class: ArgListInBraces(list) -> <desugar-class> list
  desugar-class: ArgList(list, _) -> <foldr(!([],[]), desugar-class)> list
  desugar-class: (CompForArgument(exp, _), (bases, keywords)) -> ([<desugar-expr(!Load())> exp | bases], keywords)

  
  desugar-stmt: Pass() -> Pass()
  
  desugar-stmt: e -> [<debug> ("Cannot desugar statement: ", e)]