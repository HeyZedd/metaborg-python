module CompoundStatement

imports
  
  Common
  ID
  Expression
  Program
  Statement
  SimpleStatement
  Parameters
  Test

context-free start-symbols

  CompoundStatement

context-free syntax

    CompoundStatement.If = "if" Test IndentOpen body:Suite Elif* Else?   { layout (indent "if"    body) }
    CompoundStatement.While = "while" Test IndentOpen body:Suite Else?   { layout (indent "while" body) }
    CompoundStatement.For = For
    CompoundStatement.Try = "try" IndentOpen body:Suite Except+ Else? Finally?
    CompoundStatement.TryFinally = "try" IndentOpen body:Suite Finally
    CompoundStatement.With = With
    CompoundStatement.FuncDef =  FuncDef
    CompoundStatement.ClassDef = "class" ID ArgListInBraces? IndentOpen body:Suite { layout (indent "class" body) }
    CompoundStatement.Decorated = Decorators Definition
    CompoundStatement.AsyncFunc = Async FuncDef
    CompoundStatement.AsyncWith = Async With
    CompoundStatement.AsyncFor = Async For
    
    With.With = "with" { WithItem ","}+ IndentOpen body:Suite

    For.For = "for" ExpList "in" TestList IndentOpen body:Suite Else?   { layout (indent "for" body) }
    
    Finally.Finally = "finally" IndentOpen Suite

// TODO Default Except Clause
    Except.Except = ExceptClause IndentOpen Suite
    
    ExceptClause.ExceptClause = "except" ExceptArgument?

    ExceptArgument.ExceptArgument = Test AsName?

    Decorators.Decorators = Decorator+
    
    Decorator.Decorator = "@" DottedName ArgListInBraces? NewLine

    ArgListInBraces.ArgListInBraces = "(" ArgList? ")"

    Definition.FuncDef = FuncDef
    Definition.AsyncFuncDef = AsyncFuncDef
    
    AsyncFuncDef.AsyncFuncDef = Async FuncDef
    
    FuncDef.FuncDef = "def" ID Parameters FuncType? IndentOpen Suite
    
    SubscriptList.SubscriptList = {Subscript ","}+ Comma?
    
    Subscript.Test = Test
    Subscript.Slice = Test? ":" Test? SliceOp?
    
    SliceOp.SliceOp = ":" Test?
    
    ArgList.ArgList = {Argument ","}+  Comma? 

    Argument.CompForArgument = Test CompFor?
    Argument.Assignment = Test "=" Test
    Argument.Kwargs = "**" Test
    Argument.Args = "*" Test

    CompFor.CompFor = Async? "for" ExpList "in" OrTest CompIter?
    
    CompIf.CompIf = "if" TestNocond CompIter?

    CompIter.CompFor = CompFor

    WithItem.Item = Test WithItemAs?
    WithItemAs.As = "as" Exp

    Elif.Elif = NewLine "elif" Test IndentOpen body:Suite { layout (indent "elif" body) }
    
    Else.Else = NewLine "else" IndentOpen body:Suite { layout (indent "else" body) }
    
    FuncType.FuncType = "->" Test

    Suite.Statement = SimpleStatement   {prefer}
    Suite.BlockLine = {Statement "\n"}+ { layout (align 1) }
